plugins {
    id 'java'
    id 'java-library'
    id 'io.qameta.allure' version '2.8.1'
    id "org.gradle.test-retry" version "1.1.9"
}

allure {
    autoconfigure = true
    version = '2.13.1'

    useJUnit5 {
        version = '2.13.1'
    }
}

group 'org.example'
version '1.0-SNAPSHOT'

dependencies {
    testImplementation project(':test-framework')
}

test {
    useJUnitPlatform()

    def par = System.getProperty('par')
    if (par != null) {
        par = Integer.valueOf(par)
        maxParallelForks = par
    }

    def retryCount = System.getProperty('retry')
    if (retryCount != null) {
        retryCount = Integer.valueOf(retryCount)
        retry {
            maxRetries = retryCount
            maxFailures = 20
            failOnPassedAfterRetry = false
        }
    }

    def suite = System.getProperty('suite')
    if (suite != null) {
        if (suite == "basic") {
            filter {
                includeTestsMatching "*.basic.*"
            }
        }
        if (suite == "limits") {
            filter {
                includeTestsMatching "*.limits.*"
            }
        }
        if (suite == "negative") {
            filter {
                includeTestsMatching "*.negative.*"
            }
        }
    }

    def name = System.getProperty('name')
    if (name != null) {
        filter {
            includeTestsMatching name
        }
    }
}